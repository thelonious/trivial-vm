// java -jar beaver.jar -T -w tvm-assembler.grammar
%package "com.kevlindev.tvm";

%import "com.kevlindev.tvm.ast.*";

%class "TVMParser";

%terminals BRK, LOAD, INC, BNE;
%terminals REGISTER, NUMBER, LABEL, IDENTIFIER;
%terminals COMMA;

%typeof Statement = "Instruction";
%typeof REGISTER, NUMBER, LABEL, IDENTIFIER = "String";

%goal Grammar;

Grammar
	=	Statements
	;

Statements
	=	Statements StatementWithLabel
	|	StatementWithLabel
	;

StatementWithLabel
	=	LABEL.l Statement.s
		{:
			s.setLabel(l);
			
			return s;
		:}
	|	Statement
	;

Statement
	=	LOAD REGISTER.r COMMA NUMBER.n
		{:
			return new Instruction(Opcode.LOAD, new Operand(OperandType.REGISTER, r), new Operand(OperandType.NUMBER, n));
		:}
	|	INC REGISTER.r
		{:
			return new Instruction(Opcode.INC, new Operand(OperandType.REGISTER, r));
		:}
	|	BNE IDENTIFIER.i
		{:
			return new Instruction(Opcode.BNE, new Operand(OperandType.LABEL, i));
		:}
	|	BRK
		{:
			return new Instruction(Opcode.BRK);
		:}
	;
