// java -jar beaver.jar -T -w tvm-assembler.grammar
%package "com.kevlindev.tvm.assembler";

%import "com.kevlindev.tvm.assembler.ast.Instruction";
%import "com.kevlindev.tvm.assembler.ast.Opcode";
%import "com.kevlindev.tvm.assembler.ast.Operand";
%import "com.kevlindev.tvm.assembler.ast.OperandType";

%class "TVMParser";

%terminals BRK, LOAD, INC, BNE;
%terminals ADDRESS, REGISTER, NUMBER, LABEL, IDENTIFIER;
%terminals COMMA, LBRACKET, RBRACKET, PLUS;

%typeof Statement = "Instruction";
%typeof Container, Source, Destination = "Operand";
%typeof ADDRESS, REGISTER, NUMBER, LABEL, IDENTIFIER = "String";

%goal Grammar;

Grammar
	=	Statements
	;

Statements
	=	Statements StatementWithLabel
	|	StatementWithLabel
	;

StatementWithLabel
	=	LABEL.l Statement.s
		{:
			s.setLabel(l);
			
			return s;
		:}
	|	Statement
	;

Statement
	=	LOAD Container.c COMMA Source.s
		{:
			return new Instruction(Opcode.LOAD, c, s);
		:}
	|	INC Container.c
		{:
			return new Instruction(Opcode.INC, c);
		:}
	|	BNE Destination.d
		{:
			return new Instruction(Opcode.BNE, d);
		:}
	|	BRK
		{:
			return new Instruction(Opcode.BRK);
		:}
	;

Container
	=	ADDRESS.a
		{:
			return new Operand(OperandType.ADDRESS, a);
		:}
	|	ADDRESS.a PLUS REGISTER.r
		{:
			return new Operand(OperandType.ADDRESS_INDEXED, a, r);
		:}
	|	LBRACKET ADDRESS.a RBRACKET
		{:
			return new Operand(OperandType.ADDRESS_INDIRECT, a);
		:}
	|	LBRACKET ADDRESS.a PLUS REGISTER.r RBRACKET
		{:
			return new Operand(OperandType.ADDRESS_INDIRECT_PRE_INDEXED, a, r);
		:}
	|	LBRACKET ADDRESS.a RBRACKET PLUS REGISTER.r
		{:
			return new Operand(OperandType.ADDRESS_INDIRECT_POST_INDEXED, a, r);
		:}
	|	REGISTER.r
		{:
			return new Operand(OperandType.REGISTER, r);
		:}
	;

Source
	=	Container
	|	NUMBER.n
		{:
			return new Operand(OperandType.NUMBER, n);
		:}
	;

Destination
	=	ADDRESS.a
		{:
			return new Operand(OperandType.ADDRESS, a);
		:}
	|	IDENTIFIER.i
		{:
			return new Operand(OperandType.LABEL, i);
		:}
	;
